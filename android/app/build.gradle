plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
    id "com.google.devtools.ksp"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

android {
    namespace 'com.github.thebiglettuce.azari'
    compileSdk 34
    ndkVersion "26.1.10909125"

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    kotlin { jvmToolchain(17) }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        multiDexEnabled true
        // TODO: Specify your own unique Application ID (https://developer.android.com/studio/build/application-id.html).
        applicationId 'com.github.thebiglettuce.azari'
        // You can update the following values to match your application needs.
        // For more information, see: https://docs.flutter.dev/deployment/android#reviewing-the-build-configuration.
        minSdkVersion 30
        targetSdkVersion 34
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true

            signingConfig signingConfigs.release
            manifestPlaceholders = [applicationLabel: "Azari"]

            proguardFiles(
                    // Includes the default ProGuard rules files that are packaged with
                    // the Android Gradle plugin. To learn more, go to the section about
                    // R8 configuration files.
                    getDefaultProguardFile("proguard-android-optimize.txt"),
            )
        }
        debug {
            debuggable true
            applicationIdSuffix '.debug'
            manifestPlaceholders = [applicationLabel: "Azari Debug"]
        }
        profile {
            applicationIdSuffix '.profile'
            manifestPlaceholders = [applicationLabel: "Azari Profile"]
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation 'com.github.piasy:BigImageViewer:1.8.1'
    implementation 'com.github.piasy:GlideImageLoader:1.8.1'
    implementation 'com.github.piasy:GlideImageViewFactory:1.8.1'
    implementation 'com.github.bumptech.glide:annotations:4.14.2'
    implementation 'com.github.bumptech.glide:glide:4.14.2'
    ksp 'com.github.bumptech.glide:ksp:4.14.2'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.window:window:1.3.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.8.5'
    implementation 'androidx.window:window-java:1.3.0'
    implementation "androidx.activity:activity-ktx:1.9.2"
    implementation "androidx.documentfile:documentfile:1.0.1"
    implementation "com.squareup.okio:okio:3.6.0"
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs_nio:2.1.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$KOTLIN_VERSION"
}
